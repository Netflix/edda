// Establish version and status
ext.githubProjectName = name
group = "com.netflix.${githubProjectName}"

buildscript {
    repositories { mavenCentral() }
    apply from: file('gradle/buildscript.gradle'), to: buildscript 
}

allprojects {
    repositories { mavenCentral() }
}

apply from: file('gradle/convention.gradle')
apply from: file('gradle/maven.gradle')
apply from: file('gradle/check.gradle')
apply from: file('gradle/license.gradle')
apply from: file('gradle/release.gradle')

apply plugin: 'war'
apply plugin: 'jetty'
apply plugin: 'eclipse'
apply plugin: 'idea'

dependencies {
    compile 'javax.ws.rs:jsr311-api:1.1.1'
    compile 'com.sun.jersey:jersey-core:1.11'
    compile 'com.sun.jersey:jersey-server:1.11'
    compile 'com.sun.jersey:jersey-servlet:1.11'
    compile 'org.slf4j:slf4j-api:1.6.4'
    compile('com.amazonaws:aws-java-sdk:1.3.11') {
        exclude group:'org.codehaus.jackson'
    }
    compile 'joda-time:joda-time:2.0'
    compile 'org.joda:joda-convert:1.2'
    compile 'org.codehaus.jackson:jackson-core-asl:1.9.2'
    compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.2'
    compile 'commons-beanutils:commons-beanutils:1.8.2'
    compile 'commons-collections:commons-collections:3.2.1'
    compile 'com.netflix.servo:servo-core:0.4.10'
    compile 'com.googlecode.java-diff-utils:diffutils:1.2.1'

    compile 'com.netflix.archaius:archaius-core:0.5.6'

    compile 'org.mongodb:mongo-java-driver:2.6.5'
    compile 'org.elasticsearch:elasticsearch:0.20.4'
    compile 'com.spatial4j:spatial4j:0.3'
    
    testCompile 'org.testng:testng:6.1.1'
    testCompile 'org.mockito:mockito-core:1.8.5'
    testCompile 'org.scalatest:scalatest_2.9.1:1.8'

    runtime 'org.slf4j:slf4j-log4j12:1.6.1'
    // needed for findbug Missing Class: org.slf4j.impl.StaticLoggerBinder
    providedCompile 'org.slf4j:slf4j-log4j12:1.6.1'

    providedCompile 'javax.servlet:servlet-api:2.5'

    // Libraries needed to run the scala tools
    scalaTools 'org.scala-lang:scala-compiler:2.9.1'
    scalaTools 'org.scala-lang:scala-library:2.9.1'

    // Libraries needed for scala api
    compile 'org.scala-lang:scala-library:2.9.1'
}

test << {
    ant.taskdef(name: 'scalatest', classname: 'org.scalatest.tools.ScalaTestAntTask', classpath: classpath.asPath)
    ant.scalatest(runpath: testClassesDir, haltonfailure: 'true', fork: 'false') {
    // ant.scalatest(runpath: testClassesDir, haltonfailure: 'true', fork: 'false', suite: 'com.netflix.edda.UtilsTest') {
        reporter(type: 'stdout')
    }
}

test.outputs.upToDateWhen { false }

import nl.javadude.gradle.plugins.license.License
tasks.withType(License).each { licenseTask ->
    licenseTask.exclude '**/*.json'
    licenseTask.exclude '**/*.properties'
}
